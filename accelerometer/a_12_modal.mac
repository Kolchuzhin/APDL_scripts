!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
!
! Filename: a_12_modal, ANSYS APDL script
! by <vladimir.kolchuzhin@ieee.org>
!
! Issue:	FE analysis: structural modal 
!		response: eigenshapes, eigenfrequencies fi
!
! Input:  *.db file generated using a_10_model.mac
! Output: *.db file
!		
! Rev.: 12.03.2021
! 	18.03.2021
!==========================================================================!
!
!
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
FINISH
/CLEAR,NOSTART
/CONTOUR,1,128
/VUP,,Z

/TITLE,3D FE model of an accelerometer
project_name='accelerometer'
!==========================================================================!
!==========================================================================!
/prep7

! resume the database from the .db file
resume,project_name,db

/TITLE,3D structural modal analysis of an accelerometer

allsel
eplot
!==========================================================================!
!==========================================================================!

!--------------------------------------------------------------------------!
!------------------------------! define BCs !------------------------------!
!--------------------------------------------------------------------------!
cmsel,s,nodes_set1,node
d,all,all,0.0
cmsel,all $allsel $eplot
!==========================================================================!
!==========================================================================!
/solu
 !OUTPR,ALL,ALL
num_mode=10			! total number of modes

antype,modal			! modal analysis
modopt,lanb,num_mode
mxpand,num_mode
 !pstress,on
solve
!==========================================================================!
!==========================================================================!
/post1

*DIM,fmod,,num_mode,1		! array num_mode x1

*DO,I,1,num_mode,1

  SET,1,I $PLDISP,1 $PLNS,U,SUM
  *GET,fmod(I),ACTIVE,,SET,FREQ

  /VIEW,,1,1,1
  /ui,copy,save,emf,graph,color,reverse,portrait,yes,iqual	! hard copy

*ENDDO
!.....................
/OUT,%project_name%_mode_fq,dat
*VWRITE,fmod(1)
(E20.12)
/OUT
!--------------------------------------------------------------------------!
/DSCALE,,AUTO 
!/DSCALE,,1	! do not scale displacements 

SET,1,1		! set 1. mode
allsel
plns,u,sum

!==========================================================================!
!==========================================================================!
!==========================================================================!
/eof

!set,list

     1  1238.3             1         1         1                  
     2  5830.9             1         2         2                  
     3  10818.             1         3         3                  
     4  15779.             1         4         4                  
     5  19143.             1         5         5                  
     6 0.16544E+06         1         6         6                  
     7 0.17713E+06         1         7         7                  
     8 0.26737E+06         1         8         8                  
     9 0.44353E+06         1         9         9                  
    10 0.48102E+06         1        10        10 
!==========================================================================!
!==========================================================================!