!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
!
! Filename: a_12_harmonic, ANSYS APDL script
! by <vladimir.kolchuzhin@ieee.org>
!
! Issue: linear harmonic structural analysis: |ui(f)|
!	    load: acceleration
!
! Input:  *.db file
! Output: *.db file
!		
! Rev.: 12.03.2021 :|
!
!==========================================================================!
!
!
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
FINISH
/CLEAR,NOSTART
/CONTOUR,1,128
/VUP,,Z

/TITLE,harmonic structural analysis of an accelerometer
project_name='accelerometer'
!==========================================================================!
!==========================================================================!
/prep7

! resume the database from the .db file
resume,project_name,db

allsel
eplot
!==========================================================================!
!==========================================================================!
! parameters:

g=9.8*1.0e+6			! length/time2: [m/s^2]->[um/s^2]

acel_x=2.0*g			! 
acel_y=2.0*g			! 
acel_z=2.0*g			! 

! frequency range
fini=  200.0			! Hz
fend=20000.0			! Hz
f_pnt=100			! number of points within range

!--------------------------------------------------------------------------!
!------------------------------! define BCs !------------------------------!
!--------------------------------------------------------------------------!
cmsel,s,nodes_set1,node
d,all,all,0.0
cmsel,all $allsel $eplot
!--------------------------------------------------------------------------!
!------------------------------! apply load !------------------------------!
!--------------------------------------------------------------------------!

ACEL,acel_x,acel_y,acel_z	! acceleration
!--------------------------------------------------------------------------!

!==========================================================================!
!==========================================================================!
!/NERR,9999999,9999999
 !key_harm=1		! Harmonic analysis: full method
 !key_harm=2		! Harmonic analysis: red. method
 !key_harm=3		! Harmonic analysis: VARIATIONAL TECHNOLOGY (VT)
 !key_harm=4		! MSM

/solu
antype,harmonic		! harmonic analysis
HROPT,FULL		! full harmonic response
HROUT,ON		! print results as amplitudes and phase angles
OUTPR,BASIC,1

HARFRQ,fini,fend   	! frequency range
NSUBST,f_pnt		! number of points within range

solve
!==========================================================================!
!==========================================================================!
/post26

!file,file,rst

!--------------------------------------------------------------------------!
! define master nodes
node1=927
node2=991
node3=NODE(0.0,0.0,0.0)
!--------------------------------------------------------------------------!
NSOL,2,node1,U,X,UX	! store UX complex solution at node1 in the variable 2
NSOL,3,node1,U,Y,UY	! store UY complex solution at node1 in the variable 3
NSOL,4,node1,U,Z,UZ	! store UZ complex solution at node1 in the variable 4
!--------------------------------------------------------------------------!
/GRID,1			! Turn grid on
/AXLAB,X,'Freq, Hz'	! X-axis label disp
/AXLAB,Y,'|Uz1|'	! Y-axis label disp

 !PLCPLX, KEY: specifies the part of a complex variable:
 !0 - Amplitude
 !1 - Phase angle
 !2 - Real part
 !3 - Imaginary part

PLCPLX,1 $PLVAR,4	! display phase angle(1) stored in the variable 4
PLCPLX,0 $PLVAR,4	! display amplitude(0) stored in the variable 4: UZ(4) at node1
!--------------------------------------------------------------------------!
!==========================================================================!
!==========================================================================!