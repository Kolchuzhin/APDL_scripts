!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
!
! Filename: a_01_model.mac, ANSYS APDL script
! by <vladimir.kolchuzhin@ieee.org>
!
! Issue:	FE modeling: 2d mesh -> 3d mesh by vdrag
!		3d mesh of a uniaxial polysilicon MEMS accelerometer
!
! Input: solid geometry *.cdb file
! Output: *.db file
!		
! Rev.: 10.03.2021
!
!==========================================================================!
!
!
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
FINISH
/CLEAR,NOSTART
/CONTOUR,1,128
/VUP,,Z

/TITLE,3d FE model of an accelerometer
project_name='accelerometer'
!==========================================================================!
!==========================================================================!
/prep7

! read a file of solid model and database information into the database
CDREAD,ALL,project_name,cdb
!==========================================================================!
!==========================================================================!
/prep7

heght=15

! mesh quality
el_size=50.0
le_size=6
le_size_z=6
!--------------------------------------------------------------------------!
!----------------------! Defines elements types !--------------------------!
!--------------------------------------------------------------------------!
ET,1,PLANE13

!ET,10,SOLID45	!  8-Node 3D
!ET,10,SOLID95	! 20-Node 3D
ET,10,SOLID185	! 20-Node 3D
!--------------------------------------------------------------------------!
!-------------------! Defines material properties !------------------------!
!--------------------------------------------------------------------------!
!EMUNIT,MKS	!
!--------------------------------------------------------------------------!
! orthotropic elastic material properties of the polysilicon film:

MP,EX,2,152.9e9/1e6	! Young's modulus, MPa=1N/um/um
MP,EY,2,152.9e9/1e6	! Young's modulus, MPa
MP,EZ,2,130.1e9/1e6	! Young's modulus, MPa

MP,NUXY,2,0.2		! Poisson's ratio
MP,NUYZ,2,0.28		! Poisson's ratio
MP,NUXZ,2,0.28		! Poisson's ratio

MP,GXY,2,79.6e9/1e6	! shear modulus, MPa
MP,GYZ,2,79.6e9/1e6	! shear modulus, MPa
MP,GXZ,2,79.6e9/1e6	! shear modulus, MPa

MP,DENS,2,2.330e-15	! density, kg/um3

!--------------------------------------------------------------------------!
!------------------------------! FEA Domain !------------------------------!
!--------------------------------------------------------------------------!
! 2D mesh

SHPP,off

 !SMRTSIZE,2		! set smart size meshing level 4 (fine)
MSHAPE,1,3D		! mesh with tetrahedral-shaped elements
 !ESIZE,el_size
 !LESIZE, NL1,le_size

TYPE,1
AMESH,ALL
!==========================================================================!
*GET,KPmax,KP,,NUM,MAX		!
*GET,LNmax,LINE,,NUM,MAX	!

K0=31 				! ***
K1=100+K0
K,K1,KX(K0),KY(K0),heght

! Z-line
L,K0,K1,le_size_z

! drag
TYPE,10 $VDRAG,all,,,,,,63	! ***

ET,1,0
!esel,s,type,,1

ESEL,s,type,,10
EMODIF,ALL,MAT,2
ALLSEL
!--------------------------------------------------------------------------!
!--------------------! Defines components for BCs !------------------------!
!--------------------------------------------------------------------------!
! anchor: x 47 49; y 49 56

! z3    z2
! o-----o
! |     |
! o-----o
! z0    z1

nsel,s,loc,z,0
nsel,r,loc,x,KX(47),KX(49)
nsel,r,loc,y,KY(49),KY(56)
cm,nodes_set1,node
 !d,all,all,0.0

cmsel,all $allsel $eplot
!==========================================================================!
!*GET,Nmax,NODE,,NUM,MAX !
!*GET,Emax,ELEM,,NUM,MAX !

!EWRITE,ET_%project_name%,dat,,0,LONG
!NWRITE,CT_%project_name%,dat,,0,LONG


SAVE,project_name,db,,ALL	! save model database: ALL, SOLU, MODEL, SAVE
!==========================================================================!
!==========================================================================!