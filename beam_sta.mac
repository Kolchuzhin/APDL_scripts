!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
! Filename: beam_sta.mac, ANSYS APDL script
! by Kolchuzhin V., MMT, ET/IT, TU Chemnitz
! <vladimir.kolchuzhin@etit.tu-chemnitz.de>
! Issue:  1. Geometric and FE modeling of rectangular cantilever:
!             3D FE-mesh -> *.db
!         2. Linear static structural analysis:
!             u(x,y,z), point load Fz
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
FINISH $/CLEAR $/CONTOUR,1,128
/VUP,,Z

/TITLE, Rectangular Beam
project_name='Rectangular_Beam'
!==========================================================================!
/PREP7

mm=1e-3
um=1e-6

!-------------------------! geometry parametrs !---------------------------!
! rectangular beam
L0_cant=23.0*mm   ! length
H1_cant= 0.6*mm
W1_cant= 5.0*mm
H2_cant= 0.6*mm
W2_cant= 5.0*mm

! mesh quality
el_size=0.15*mm

!----------------------! defines elements types !--------------------------!
ET,1,SOLID45  ! 8-Node 3D
!ET,1,SOLID95 !20-Node 3D
!ET,1,SOLID185  !20-Node 3D

!-------------------! defines material properties !------------------------!
EMUNIT,MKS	

! material properties of <110> Si:
MP,EX,  1,169e9		! Young's modulus, Pa
MP,NUXY,1,0.066		! Poisson's ratio
MP,DENS,1,2329		! density, kg/m3

!------------------------------! FEA domain !------------------------------!
K,1,-L0_cant/2,-W1_cant/2,0
K,2,-L0_cant/2,+W1_cant/2,0
K,3,-L0_cant/2,+W1_cant/2,H1_cant
K,4,-L0_cant/2,-W1_cant/2,H1_cant
K,5,+L0_cant/2,-W2_cant/2,0
K,6,+L0_cant/2,+W2_cant/2,0
K,7,+L0_cant/2,+W2_cant/2,H2_cant
K,8,+L0_cant/2,-W2_cant/2,H2_cant

V,1,2,3,4,5,6,7,8	! defines a volume through keypoints
!--------------------------------------------------------------------------!
! mesh
ESIZE,el_size
MAT,1
TYPE,1
VMESH,ALL
!--------------------! defines components for BCs !------------------------!
allsel
nsel,s,loc,x,-L0_cant/2
cm,nodes_fix,node

allsel
nsel,s,loc,x,+L0_cant/2
nsel,r,loc,y,0.0
nsel,r,loc,z,+H2_cant
cm,nodes_load,node

cmsel,all $allsel
eplot
!==========================================================================!
! apply load
m=100/1000  ! kg
f_z=m*9.8   ! N
F,nodes_load,FZ,-f_z
!--------------------------------------------------------------------------!
! apply BCs
cmsel,s,nodes_fix,node
d,all,all,0.0
cmsel,all $allsel
!--------------------------------------------------------------------------!
/solu
antype,static		! static analysis
solve
!==========================================================================!
/post1
allsel
plns,u,sum
!==========================================================================!
